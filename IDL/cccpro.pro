pro cccpro,x1,y1,x2,y2,ind1,ind2,dmin,coords=coords,dt=dt,plot=plot,grab=grab,db=db

; cccpro,x1,y1,x2,y2,ind1,ind2,dmin,dt=6.

;+
; NAME:
;	CCCPRO
; PURPOSE:
;	Nearest-Neighbor Catalogue Cross-Correlation
; USE:
;	This routine implements a simple and general "Cross-Correlation"
;	algorithm that looks for associations/counterparts between two
;	catalogues. For any given source SRC1 in some "Catalogue #1", it looks
;	for sources in some "Catalogue #2" that fall within a given search
;	radius from SRC1. If it finds any, it then takes the nearest one (SRC2)
;	and associates it with SRC1. It then (optionally) plots & grabs the
;	determined association distances in histogram form.
;
;	To run this routine, one needs defining:
;	- X1,X2,Y1,Y2 : 2-D source coordinates in CAT1 and CAT2, in either
;	RA-Dec (decimal degrees) or some fits-like X-Y (pixels) coordinate system.
;	- the employed coordinate system, COORDS='radec' or COORDS='xy'
;	(defaults to COORDS='radec')
;	- DT : the desired cross-correlation distance threshold, expressed in either
;	arcseconds (COORDS='radec') or pixels (COORDS='xy'), defaults to DT=6
;	- DB : the desired histogram plot distance bin, expressed in either
;	arcseconds (COORDS='radec') or pixels (COORDS='xy'), defaults to DB=DT/10
;	- PLOT : byte keyword toggling histogram plotting of association
;	distances, (0 = don't plot , 1 = plot , default is 0)
;	- GRAB : byte keyword toggling plot windows grabbing (! if GRAB is set, then PLOT setting is overridden and PLOT is also set !)
;	(0 = don't grab , 1 = grab , default is 0)
;	Useful arrays generated by this routine are
;	- IND1 & IND2 : indices of associated sources in CAT1 & CAT2
;	- DMIN : CAT1-CAT2 inter-catalogue distance
; HISTORY:
;	16 Nov 2004	Written by Mattia Vaccari - Per Paola...
;	05 Dec 2004	Refactoring and Debugging (Mattia Vaccari)
;	05 Jun 2005	Plotting refactoring and GRAB keyword added (Mattia Vaccari)
;	20 Sep 2005	NOX keyword added (Mattia Vaccari)
;	20 Mar 2006	Cumulative histogram now plots raw numbers rather than fractions (Mattia Vaccari)
;	05 Jul 2006	NOX keyword renamed PLOT : by default CCC now doesn't plot or grab (Mattia Vaccari)
;	19 Dec 2006	Error/Warning Messages Added & Documentation Revamped (Mattia Vaccari)
;	22 Feb 2007	Single-Window Plotting via !P.MULTI (Mattia Vaccari)
;	23 Aug 2008	Procedure-ization (Mattia Vaccari)
;-

on_error,2

; Coordinate System : Default is RA-Dec (J2000), alternative is X-Y (HDR)

if not(keyword_set(coords)) then coords='radec'

if ( coords ne 'radec' and coords ne 'xy') then begin $
  print,'Non-supported coordinate system was supplied! Stopping...' &$
  stop &$
endif

; Distance Threshold - Default is 6 arcsec for RA-Dec and 6 pixels for X-Y

if not(keyword_set(dt)) then dt = 6.

if coords eq 'radec' then dt2 = dt / 3600. else if coords eq 'xy' then dt2 = dt

; Plottin & Grabbing

if not(keyword_set(plot)) then plot=0

if not(keyword_set(grab)) then grab=0 else plot=1

; Cross-Correlation Proper between "reference" catalogue CAT1 and CAT2
; For each CAT1 source:
; - Find distances between CAT1 source and all CAT2 sources
; - Assign nearest CAT2 source to CAT1 source, provided distance is less than DT2.
; - If a counterpart was found, store indices of CAT1 and CAT2 sources and their distance.

ind1=[-1] & ind2=[-1] & dmin=[-1]

if coords eq 'radec' then begin $
  for i=0L,n_elements(x1)-1 do begin $
    dist=sqrt(((x2-x1[i])*cos(y1[i]*!pi/180.))^2+(y2-y1[i])^2) & dm=min(dist,im) &$
    if dm lt dt2 then begin &$
      ind1=[ind1,i] & ind2=[ind2,im] & dmin=[dmin,dm] &$
    endif &$
  endfor &$
endif else if coords eq 'xy' then begin &$
  for i=0L,n_elements(x1)-1 do begin $
    dist=sqrt(((x2-x1[i]))^2+(y2-y1[i])^2) & dm=min(dist,im) &$
    if dm lt dt2 then begin &$
      ind1=[ind1,i] & ind2=[ind2,im] & dmin=[dmin,dm] &$
    endif &$
  endfor &$
endif

if n_elements(ind1) gt 1 then begin $

  ind1=ind1[1:*] & ind2=ind2[1:*] & dmin=dmin[1:*] &$

  print,n_elements(ind1),' out of ',n_elements(x1),' sources in catalogue # 1' &$
  if coords eq 'radec' then print,' have (at least) a counterpart within ',dt,' arcsec in catalogue # 2' &$
  if coords eq 'xy' then print,' have (at least) a counterpart within ',dt,' pixels in catalogue # 2' &$
  print,'This amounts to a ',float(n_elements(ind1))/n_elements(x1),' fraction!!!' &$

; Association Distances HistoPlot : in arcsec or pixels, respectively

  if keyword_set(plot) then begin $
    !p.multi=[0,2,1,0,0] &$
    window,xsize=800,ysize=400 &$
    if not(keyword_set(db)) then db = dt/10. &$
; Differential
;    window,0 &$
    if coords eq 'radec' then histoplot,dmin*3600.,db,xst=2,yst=2,xtitle='Association Distance [arcsec]',ytitle='# of Associations' else if coords eq 'xy' then histoplot,dmin,db,xst=2,yst=2,xtitle='Association Distance [arcsec]',ytitle='# of Associations' &$
    if keyword_set(grab) then screen_grab,'ccc-grab1.jpg',/jpg &$
; Integral
;    window,1 &$
    if coords eq 'radec' then histoplot,dmin*3600.,db,/cum,xst=2,yst=2,xtitle='Association Distance [arcsec]',ytitle='# of Associations' else if coords eq 'xy' then histoplot,dmin,db,/cum,xst=2,yst=2,xtitle='Association Distance [arcsec]',ytitle='# of Associations' &$
    if keyword_set(grab) then screen_grab,'ccc-grab2.jpg',/jpg &$
    !p.multi=[0,0,0,0,0] &$
  endif &$

endif else print,'No (ZERO) Associations Found!?'

end
